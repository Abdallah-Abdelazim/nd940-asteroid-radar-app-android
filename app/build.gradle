plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-parcelize"
    id "androidx.navigation.safeargs"
    id "org.jetbrains.kotlin.kapt"
    id "com.google.devtools.ksp"
    id "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
}

android {
    namespace "com.abdallah_abdelazim.asteroidradar"
    compileSdk 33

    defaultConfig {
        applicationId "com.abdallah_abdelazim.asteroidradar"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    secrets {
        propertiesFileName 'secrets.properties'
    }
}

dependencies {

    // Core
    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.activity:activity-ktx:$versions.activity_ktx"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment_ktx"

    // UI
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"

    // Glide
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    ksp "com.github.bumptech.glide:ksp:$versions.glide"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"

    // Jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    // Room
    implementation "androidx.room:room-ktx:$versions.room"
    ksp "androidx.room:room-compiler:$versions.room"

    // Retrofit & Moshi
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofit"
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$versions.okhttp"))
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Chucker
    debugImplementation "com.github.chuckerteam.chucker:library:$versions.chucker"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$versions.chucker"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$versions.workmanager"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines_android"

    // Koin DI
    implementation "io.insert-koin:koin-android:$versions.koin"

    // Testing
    testImplementation "junit:junit:$versions.test_junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidtest_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}